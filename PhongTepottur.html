<!DOCTYPE html>
<html lang="en">
  <script id="vertex-shader" type="x-shader/x-vertex">
    attribute vec4 vPosition;
    attribute vec4 vNormal;
    varying vec3 N, L, E;
    uniform mat4 modelViewMatrix;
    uniform mat4 projectionMatrix;
    uniform vec4 lightPosition;
    uniform mat3 normalMatrix;
    void main()
    {
        vec3 pos = (modelViewMatrix * vPosition).xyz;
        if(lightPosition.w == 0.0) L = normalize(lightPosition.xyz);
        else L = normalize(lightPosition.xyz - pos);
        E = -normalize(pos);
        N = normalize(normalMatrix * vNormal.xyz);
        gl_Position = projectionMatrix * modelViewMatrix * vPosition;
    }
  </script>
  <script id="fragment-shader" type="x-shader/x-fragment">
    precision mediump float;
    uniform vec4 ambientProduct;
    uniform vec4 diffuseProduct;
    uniform vec4 specularProduct;
    uniform float shininess;
    uniform int usePhong;
    varying vec3 N, L, E;
    void main()
    {
        vec4 ambient = ambientProduct;
        float Kd = max(dot(normalize(L), normalize(N)), 0.0);
        vec4 diffuse = Kd * diffuseProduct;
        vec4 specular;
        if (usePhong == 1) {
            vec3 R = reflect(-normalize(L), normalize(N));
            float Ks = pow(max(dot(normalize(E), R), 0.0), shininess);
            specular = Ks * specularProduct;
        } else {
            vec3 H = normalize(normalize(L) + normalize(E));
            float Ks = pow(max(dot(normalize(N), H), 0.0), shininess);
            specular = Ks * specularProduct;
        }
        if(dot(normalize(L), normalize(N)) < 0.0) specular = vec4(0.0, 0.0, 0.0, 1.0);
        vec4 fColor = ambient + diffuse + specular;
        fColor.a = 1.0;
        gl_FragColor = fColor;
    }
  </script>
  <script type="text/javascript" src="./Common/webgl-utils.js"></script>
  <script type="text/javascript" src="./Common/initShaders.js"></script>
  <script type="text/javascript" src="./Common/MV.js"></script>
  <script type="text/javascript" src="teapotData.js"></script>
  <script type="text/javascript" src="PhongTepottur.js"></script>
  <body>
    <div>
      <button id="changeState">Blinn-Phong</button>
    </div>
    <canvas id="gl-canvas" width="600" height="600"></canvas>
  </body>
</html>
